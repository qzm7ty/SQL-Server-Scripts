--18/03/2013 Script altered so that only ONLINE databases are selected. JGi

USE MASTER
GO

SET NOCOUNT ON;

SELECT ROW_NUMBER() OVER(ORDER BY name) Seq, name Bank
INTO #Databases
FROM sys.databases
WHERE name NOT IN ('master', 'model', 'msdb', 'tempdb', 'ReportServerTempDB')
AND state = 0
AND compatibility_level > 80
ORDER BY Bank;

DECLARE
	@Loop INT = 1,
	@Qt INT = (SELECT COUNT(1) FROM #Databases),
	@dbname VARCHAR(100);

	WHILE @Loop <= @Qt
	 BEGIN
	  SET @dbname = (SELECT Bank FROM #Databases WHERE Seq = @Loop);
		EXEC( 
		'USE ' + '[' + @dbname + ']' + '; ' +
		'PRINT ''Qty affected indexes: '' + db_name();
		SELECT
			ROW_NUMBER() OVER(ORDER BY p.object_id, p.index_id) Seq,
   			t.name aTable, h.name aschema,
			i.name aIndex, p.avg_fragmentation_in_percent Frag
		INTO #Consulta
		FROM
		sys.dm_db_index_physical_stats(DB_ID(),null,null,null,null) p
		join sys.indexes i on (p.object_id = i.object_id and p.index_id = i.index_id)
		join sys.tables t on (p.object_id = t.object_id)
		join sys.schemas h on (t.schema_id = h.schema_id)
		where p.avg_fragmentation_in_percent > 10.0
		and p.index_id > 0
		and p.page_count >= 10
		ORDER BY aschema, aTable;
		DECLARE
			@Loop INT = 1,
			@Total INT = (SELECT COUNT(1) FROM #Consulta),
			@Command VARCHAR(500)
		WHILE @Loop <= @Total
			BEGIN
				SELECT @Command = ''ALTER INDEX '' + ''['' + aIndex + '']'' +
					'' ON '' + aSchema + ''.'' + aTable +
					( CASE WHEN Frag > 30.0 THEN '' REBUILD'' ELSE '' REORGANIZE'' END)
					FROM #Consulta
					WHERE Seq = @Loop;
				EXEC(@Command);
				PRINT ''Execute: '' + @Command;
				SET @Loop = @Loop + 1;
			END;
		PRINT DB_NAME() + '' Qty of Affected Indexes: '' + CONVERT(VARCHAR(5),@Total);
		PRINT ''-----'';
		DROP TABLE #Consulta;');  
	  SET @Loop = @Loop + 1;
	 END;

DROP TABLE #Databases;
